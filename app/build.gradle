apply plugin: 'net.twisterrob.android-kotlin-app'

dependencies {
	implementation project(':feature:base')
	implementation project(':feature:image')
	implementation project(':feature:lighting')
	implementation project(':feature:porterduff')
	implementation project(':feature:matrix')
	implementation project(':feature:palette')
	implementation project(':feature:resfont')
	implementation project(':feature:keyboard')
	implementation project(':feature:about')
}

dependencies {
	androidTestImplementation project(':test-base')
}

android {
	defaultConfig {
		applicationId 'net.twisterrob.colorfilters'

		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}
	sourceSets {
		androidTest {
			configurations['implementation'].dependencies.withType(ProjectDependency) {
				evaluationDependsOn it.dependencyProject.path // get .android ready
				java.srcDirs it.dependencyProject.android.sourceSets.androidTest.java.srcDirs.toArray()
				kotlin.srcDirs it.dependencyProject.android.sourceSets.androidTest.kotlin.srcDirs.toArray()
			}
		}
	}
}

tasks.withType(JavaCompile) {
	it.options.compilerArgs << '-Xlint:all' << '-Werror'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
	kotlinOptions {
		allWarningsAsErrors = true
	}
}
